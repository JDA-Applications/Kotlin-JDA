/*
 *     Copyright 2016 - 2017 Florian Spie√ü
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    ext.kotlin_version = '1.1.1'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.9'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'club.minnced'
version = '0.BETA.1'
def jda_version = '3.0.0_165'

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    examples {
        compileClasspath = main.output
        runtimeClasspath = main.output
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.14'

    // exclude when published
    compileOnly "net.dv8tion:JDA:$jda_version"

    sourceSets.examples.compileClasspath += configurations.compile + configurations.compileOnly
    sourceSets.examples.runtimeClasspath += configurations.runtime

    sourceSets.test.compileClasspath += sourceSets.examples.compileClasspath
    sourceSets.test.runtimeClasspath += sourceSets.examples.runtimeClasspath
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    logging.level = LogLevel.QUIET
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/dokkaJavadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from "$buildDir/sources"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

task sources(type: Copy) {
    from 'src/main/kotlin'
    into "$buildDir/sources"
}

compileJava {
    dependsOn sources
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn javadocJar
    dependsOn sourcesJar
    dependsOn shadowJar

    jar.mustRunAfter clean
    javadocJar.mustRunAfter jar
    sourcesJar.mustRunAfter javadocJar
    shadowJar.mustRunAfter sourcesJar
}

wrapper {
    gradleVersion = '3.4.1'
}

jar {
    def time = OffsetDateTime.now(ZoneId.of("UTC"))
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Created-At': time.format(DateTimeFormatter.ISO_OFFSET_DATE_TIME)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.JDA-Applications'
            artifactId 'Kotlin-JDA'
            version project.version

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            artifact jar {
                classifier ''
            }
            artifact shadowJar {
                classifier 'withDependencies'
            }
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
